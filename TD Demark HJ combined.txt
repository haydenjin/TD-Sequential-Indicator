//@version=3


// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © haydenjin
// 
// All uses of this software is for educational and entertainment purposes only
// References: DeMark Indicators book by Jason Perl
//

study(title="TD Sequential", shorttitle="Sequential", overlay=true)

//------------------------------------------------
//------------------------------------------------
//---- USER INPUT SECTION ------------------------
//------------------------------------------------
//------------------------------------------------

PriceSource = input(title="Price: Source", type=source, defval=close)
SetupBars = input(title="Setup: Bars", type=integer, defval=9, minval=4, maxval=31)
SetupLookback = input(title="Setup: Lookback Bars", type=integer, defval=4, minval=1, maxval=14)
SetupEqualEnable = input(title="Setup: Include Equal Price", type=bool, defval=false)
SetupPerfLookback = input(title="Setup: Perfected Lookback", type=integer, defval=3, minval=1, maxval=14)
SetupShowCount = input(title="Setup: Show Count", type=bool, defval=true)
SetupTrendExtend = input(title="Setup Trend: Extend", type=bool, defval=false)
SetupTrendShow = input(title="Setup Trend: Show", type=bool, defval=true)
CntdwnBars = input(title="Countdown: Bars", type=integer, defval=13, minval=3, maxval=31)
CntdwnLookback = input(title="Countdown: Lookback Bars", type=integer, defval=2, minval=1, maxval=30)
CntdwnQualBar = input(title="Countdown: Qualifier Bar", type=integer, defval=8, minval=3, maxval=30)
CntdwnAggressive = input(title="Countdown: Aggressive", type=bool, defval=false)
CntdwnShowCount = input(title="Countdown: Show Count", type=bool, defval=true)
RiskLevelShow = input(title="Risk Level: Show", type=bool, defval=false)
Transp = input(title="Transparency", type=integer, defval=0, minval=0, maxval=100)

PlotEditEnable = false


//------------------------------------------------
//------------------------------------------------
//---- PROGRAM LOGIC SECTION ---------------------
//------------------------------------------------
//------------------------------------------------


//---- TD Price Flips ---------------------------
// Plot parameters
// Price Equal plot (pep). Uses plotshape()
pepColor=gray, pepLoc=location.belowbar, pepSize=size.auto, pepStyle=shape.circle

// Create impulse series of price action. Compare where price is greater/less/equal than prior price.
setupPriceUp = (PriceSource > PriceSource[SetupLookback])
setupPriceDown = (PriceSource < PriceSource[SetupLookback])
setupPriceEqual = (PriceSource == PriceSource[SetupLookback])


//---- TD Setups ---------------------------
// Plot parameters
// Setup Count plot (scp)
scpShowLast=144
scupColor=green, scupLoc=location.abovebar, scupSize=size.auto
scdpColor=green,   scdpLoc=location.belowbar, scdpSize=size.auto
// Setup Count Up/Down > Last plot (scuglp/scdglp). Uses plotshape()
scuglpColor=green, scuglpLoc=location.abovebar, scuglpSize=size.auto, scuglpStyle=shape.diamond, scuglpText=""
scdglpColor=green, scdglpLoc=location.belowbar, scdglpSize=size.auto, scdglpStyle=shape.diamond, scdglpText=""
// Setup Sell plot (ssp), Setup Buy plot (sbp). Uses plotshape()
sspLoc=location.abovebar, sspSize=size.tiny, sspText="9"
sbpLoc=location.belowbar, sbpSize=size.tiny, sbpText="9"
// Setup Sell/Buy Perfected plot (sspp/sbpp). Uses plotshape()
ssppLoc=location.abovebar, ssppSize=size.small, ssppText="▼"
sbppLoc=location.belowbar, sbppSize=size.small, sbppText="▲"

setupCountUp = na
setupCountUp := SetupEqualEnable ?
   (setupPriceUp or (setupCountUp[1] and setupPriceEqual)) ? nz(setupCountUp[1])+1 : 0
   :
   setupPriceUp ? nz(setupCountUp[1])+1 : 0
setupCountDown = na
setupCountDown := SetupEqualEnable ?
   (setupPriceDown or (setupCountDown[1] and setupPriceEqual)) ? nz(setupCountDown[1])+1 : 0
   :
   setupPriceDown ? nz(setupCountDown[1])+1 : 0


setupSell = na
setupSell := setupCountUp==SetupBars ? valuewhen(setupCountUp==SetupBars, PriceSource, 0) : na
setupBuy = na
setupBuy := setupCountDown==SetupBars ? valuewhen(setupCountDown==SetupBars, PriceSource, 0) : na

// Count bars between setups... used by other indicators
setupSellCount = barssince(setupSell)
setupBuyCount = barssince(setupBuy)

// Perfected Setups
setupIsPerfected = 2 
setupIsDeferred = 1 

// Perfected Sell Setup events
// Get the price for which Sell Setup perfection is evaluated. Stair-step series.
setupSellPerfPrice = na
setupSellPerfPrice := setupCountUp==SetupBars ?
   ((valuewhen(setupCountUp==(SetupBars-SetupPerfLookback), high, 0) >= 
      valuewhen(setupCountUp==(SetupBars-SetupPerfLookback+1), high, 0)) ? 
         valuewhen(setupCountUp==(SetupBars-SetupPerfLookback), high, 0 ) :
         valuewhen(setupCountUp==(SetupBars-SetupPerfLookback+1), high, 0 )
   ) : nz(setupSellPerfPrice[1])

setupSellPerfMask = na
setupSellPerfMask := 
   ((nz(setupSellPerfMask[1])>=setupIsPerfected) or (not na(setupBuy))) ? na : 
      setupCountUp==SetupBars ? 
         ((valuewhen(setupCountUp==(SetupBars-1), high, 0) >= setupSellPerfPrice) or 
          (valuewhen(setupCountUp==SetupBars, high, 0) >= setupSellPerfPrice)) ?
             setupIsPerfected : setupIsDeferred 
         : 
         na(setupSellPerfMask[1]) ? na : 
           high>=setupSellPerfPrice ? setupIsPerfected : setupSellPerfMask[1]

// Get the perfected bar for plotting later
setupSellPerf = setupSellPerfMask==setupIsPerfected ? PriceSource : na

// Perfected Buy Setup events
setupBuyPerfPrice = na
setupBuyPerfPrice := setupCountDown==SetupBars ?
   ((valuewhen(setupCountDown==(SetupBars-SetupPerfLookback), low, 0) <= 
      valuewhen(setupCountDown==(SetupBars-SetupPerfLookback+1), low, 0)) ? 
         valuewhen(setupCountDown==(SetupBars-SetupPerfLookback), low, 0 ) :
         valuewhen(setupCountDown==(SetupBars-SetupPerfLookback+1), low, 0 )
   ) : nz(setupBuyPerfPrice[1])
//plot(setupBuyPerfPrice, color=yellow, linewidth=2)  // debug

setupBuyPerfMask = na
setupBuyPerfMask := 
   ((nz(setupBuyPerfMask[1])>=setupIsPerfected) or (not na(setupSell))) ? na : 
      setupCountDown==SetupBars ? 
         ((valuewhen(setupCountDown==(SetupBars-1), low, 0) <= setupBuyPerfPrice) or 
          (valuewhen(setupCountDown==SetupBars, low, 0) <= setupBuyPerfPrice)) ?
             setupIsPerfected : setupIsDeferred 
         : 
         na(setupBuyPerfMask[1]) ? na : 
           low<=setupBuyPerfPrice ? setupIsPerfected : setupBuyPerfMask[1]

setupBuyPerf = setupBuyPerfMask==setupIsPerfected ? PriceSource : na


// TDST
// Plot parameters
// Setup Trend Support/Resistance plot (stsp/strp). Uses plot()
stspColor=#99FF99, stspStyle=circles, stspOffset=0 //=(1-SetupBars)
strpColor=#FF9999, strpStyle=circles, strpOffset=0 //=(1-SetupBars)
// Shading between support/resistance lines. Uses fill()
stpColorNormal=#00000000   // Support is below resistance (#00000000 = no color and transparent)
stpColorFlip  =#20202040   // Support is above resistance, flipped

setupTrendSupport = na
setupTrendSupport := setupSell ? 
   (SetupTrendExtend ? (
      setupSellCount[1] <=   SetupBars ? lowest(SetupBars) : 
      setupSellCount[1] <= 2*SetupBars ? lowest(2*SetupBars) :
      setupSellCount[1] <= 3*SetupBars ? lowest(3*SetupBars) :
      setupSellCount[1] <= 4*SetupBars ? lowest(4*SetupBars) :
      setupSellCount[1] <= 5*SetupBars ? lowest(5*SetupBars) :
      setupSellCount[1] <= 6*SetupBars ? lowest(6*SetupBars) :
      setupSellCount[1] <= 7*SetupBars ? lowest(7*SetupBars) :
      setupSellCount[1] <= 8*SetupBars ? lowest(8*SetupBars) :
      setupSellCount[1] <= 9*SetupBars ? lowest(9*SetupBars) : lowest(10*SetupBars))
      : lowest(SetupBars) )
   : nz(setupTrendSupport[1])
setupTrendResist = na
setupTrendResist := setupBuy ? 
   (SetupTrendExtend ? (
      setupBuyCount[1] <=   SetupBars ? highest(SetupBars) : 
      setupBuyCount[1] <= 2*SetupBars ? highest(2*SetupBars) : 
      setupBuyCount[1] <= 3*SetupBars ? highest(3*SetupBars) : 
      setupBuyCount[1] <= 4*SetupBars ? highest(4*SetupBars) : 
      setupBuyCount[1] <= 5*SetupBars ? highest(5*SetupBars) : 
      setupBuyCount[1] <= 6*SetupBars ? highest(6*SetupBars) : 
      setupBuyCount[1] <= 7*SetupBars ? highest(7*SetupBars) : 
      setupBuyCount[1] <= 8*SetupBars ? highest(8*SetupBars) : 
      setupBuyCount[1] <= 9*SetupBars ? highest(9*SetupBars) : highest(10*SetupBars))
      : highest(SetupBars))
   : nz(setupTrendResist[1])


//---- TD Countdown ---------------------------
// Plot parameters
// Countdown Count Up/Down plot (ccup/ccdp). Uses plotshape()
ccupColor=na, ccupLoc=location.abovebar, ccupSize=size.auto, ccupStyle=shape.circle  // no text
ccdpColor=na,   ccdpLoc=location.belowbar, ccdpSize=size.auto, ccdpStyle=shape.circle  // no text 
// Countdown Last Count Up/Down plot (clcup/clcdp). Uses plotshape()
clcupColor=#33FF33, clcupLoc=location.belowbar, clcupSize=size.auto, clcupStyle=shape.circle  // no text
clcdpColor=#FF8333, clcdpLoc=location.belowbar, clcdpSize=size.auto, clcdpStyle=shape.circle  // no text
// Countdown Qualification Count plot (cqcp). Uses plotshape() ...only one defintion for both up/down counts
cqcpColor=#4444FF, cqcpLoc=location.belowbar, cqcpSize=size.auto, cqcpStyle=shape.diamond, cqcpText="Q"
// Countdown Count Recycle plot (cqcp). Uses plotshape() ...only one defintion for both up/down counts
ccrpColor=white, ccrpLoc=location.belowbar, ccrpSize=size.auto, ccrpStyle=shape.cross, ccrpText="R"
// Countdown Sell/Buy Deferred plot (csdp/cbdp). Uses plotshape()
csdpColor=green, csdpLoc=location.belowbar, csdpSize=size.auto, csdpStyle=shape.cross, csdpText=""
cbdpColor=red,   cbdpLoc=location.belowbar, cbdpSize=size.auto, cbdpStyle=shape.cross, cbdpText=""
// Countdown Sell/Buy event plot (csp/cbp). Uses plotshape()
cspColor=red, cspLoc=location.abovebar, cspSize=size.tiny, cspStyle=shape.arrowdown, cspText="13"
cbpColor=red, cbpLoc=location.belowbar, cbpSize=size.tiny, cbpStyle=shape.arrowup,   cbpText="13"
// Countdown Sell/Buy Aggressive event plot (csap/cbap)... same as csp/cbp, except text
csapText="aggr\ncount\ndown"
cbapText="aggr\ncount\ndown"

// Compare where price is greater/less than prior price
cntdwnPriceUp = CntdwnAggressive ? (high >= high[CntdwnLookback]) : (PriceSource >= high[CntdwnLookback])
cntdwnPriceDown = CntdwnAggressive ? (low <= low[CntdwnLookback]) : (PriceSource <= low[CntdwnLookback])

// Determine Setup recycle events
cntdwnCountUpRecycle = na
cntdwnCountUpRecycle := (setupCountUp==(2*SetupBars)) ? valuewhen((setupCountUp==(2*SetupBars)), PriceSource, 0) : na
cntdwnCountDownRecycle = na
cntdwnCountDownRecycle := (setupCountDown==(2*SetupBars)) ? valuewhen((setupCountDown==(2*SetupBars)), PriceSource, 0) : na

cntdwnCountUp = na
cntdwnCountUp := na(cntdwnPriceUp) ? cntdwnCountUp[1] :
   ((not na(setupBuy)) or (PriceSource<setupTrendSupport) or (not na(cntdwnCountUpRecycle))) ? na :
      ((not na(setupSell)) ? (cntdwnPriceUp ? 1 : 0) :
         na(cntdwnCountUp[1]) ? na : (cntdwnPriceUp ? cntdwnCountUp[1]+1 : cntdwnCountUp[1])
      )
// Convert stair-step values to impulse, i.e. only keep the initial price move
cntdwnCountUpImp = na
cntdwnCountUpImp := cntdwnPriceUp ? cntdwnCountUp : na

cntdwnCountDown = na
cntdwnCountDown := na(cntdwnPriceDown) ? cntdwnCountDown[1] :
   ((not na(setupSell)) or (PriceSource>setupTrendResist) or (not na(cntdwnCountDownRecycle))) ? na :
      ((not na(setupBuy)) ? (cntdwnPriceDown ? 1 : 0) :
         na(cntdwnCountDown[1]) ? na : (cntdwnPriceDown ? cntdwnCountDown[1]+1 : cntdwnCountDown[1])
      )
// plot(cntdwnCountDown, color=yellow, linewidth=2)  // debug
cntdwnCountDownImp = na
cntdwnCountDownImp := cntdwnPriceDown ? cntdwnCountDown : na


cntdwnIsQualified = 2 
cntdwnIsDeferred = 1 

cntdwnSellQualPrice = na
cntdwnSellQualMask = na
cntdwnSellQualMaskImp = na
cntdwnBuyQualPrice = na
cntdwnBuyQualMask = na
cntdwnBuyQualMaskImp = na

if (CntdwnQualBar < CntdwnBars)
    // Get the price at CntdwnQualBar, create stair-step function of value
    cntdwnSellQualPrice := cntdwnCountUpImp==CntdwnQualBar ? 
       valuewhen(cntdwnCountUpImp==CntdwnQualBar, PriceSource, 0) : nz(cntdwnSellQualPrice[1])
    cntdwnSellQualMask := 
       (nz(cntdwnSellQualMask[1])>=cntdwnIsQualified) or na(cntdwnCountUp) ? na :
          (cntdwnCountUpImp==CntdwnBars ?
             (valuewhen(cntdwnCountUpImp==CntdwnBars, high, 0) >= cntdwnSellQualPrice ? 
               cntdwnIsQualified : cntdwnIsDeferred) : 
             (na(cntdwnSellQualMask[1]) ? na :
               (cntdwnCountUpImp>CntdwnBars ?
                  (valuewhen(cntdwnCountUpImp>CntdwnBars, high, 0) >= cntdwnSellQualPrice ? 
                     cntdwnIsQualified : cntdwnSellQualMask[1]) :
                  cntdwnSellQualMask[1])))

    cntdwnBuyQualPrice := cntdwnCountDownImp==CntdwnQualBar ? 
      valuewhen(cntdwnCountDownImp==CntdwnQualBar, PriceSource, 0) : nz(cntdwnBuyQualPrice[1])
    cntdwnBuyQualMask := 
       (nz(cntdwnBuyQualMask[1])>=cntdwnIsQualified) or na(cntdwnCountDown) ? na :
          (cntdwnCountDownImp==CntdwnBars ?
             (valuewhen(cntdwnCountDownImp==CntdwnBars, low, 0) <= cntdwnBuyQualPrice ? 
               cntdwnIsQualified : cntdwnIsDeferred) : 
             (na(cntdwnBuyQualMask[1]) ? na :
               (cntdwnCountDownImp>CntdwnBars ?
                  (valuewhen(cntdwnCountDownImp>CntdwnBars, low, 0) <= cntdwnBuyQualPrice ? 
                     cntdwnIsQualified : cntdwnBuyQualMask[1]) :
                  cntdwnBuyQualMask[1])))
else
    cntdwnSellQualMask := cntdwnCountUp==CntdwnBars ? cntdwnIsQualified : na
    cntdwnBuyQualMask := cntdwnCountDown==CntdwnBars ? cntdwnIsQualified : na

// Get the impulse version of the stair-step qualification mask
cntdwnSellQualMaskImp := cntdwnCountUpImp ? cntdwnSellQualMask : na
cntdwnBuyQualMaskImp := cntdwnCountDownImp ? cntdwnBuyQualMask : na

cntdwnSell = na
cntdwnSellDefer = na
cntdwnSell := cntdwnSellQualMaskImp==cntdwnIsQualified ? 
   valuewhen(cntdwnSellQualMaskImp==cntdwnIsQualified, PriceSource, 0) : na
cntdwnSellDefer := cntdwnSellQualMaskImp==cntdwnIsDeferred ? 
   valuewhen(cntdwnSellQualMaskImp==cntdwnIsDeferred, PriceSource, 0) : na

cntdwnBuy = na
cntdwnBuyDefer = na
cntdwnBuy := cntdwnBuyQualMaskImp==cntdwnIsQualified ? 
   valuewhen(cntdwnBuyQualMaskImp==cntdwnIsQualified, PriceSource, 0) : na
cntdwnBuyDefer := cntdwnBuyQualMaskImp==cntdwnIsDeferred ? 
   valuewhen(cntdwnBuyQualMaskImp==cntdwnIsDeferred, PriceSource, 0) : na

// TD Risk Levels for setup and countdown
riskLevel = na, riskBar = na
//riskBar :=
//   setupSell ? highest(SetupBars)
riskLevel :=
   setupSell or cntdwnCountUpRecycle ? (highest(SetupBars)+valuewhen(high==highest(SetupBars), tr, 0)) :
   setupBuy or cntdwnCountDownRecycle ? (lowest(SetupBars)-valuewhen(low==lowest(SetupBars), tr, 0)) :
   cntdwnSell ? (highest(CntdwnBars)+valuewhen(high==highest(CntdwnBars), tr, 0)) :
   cntdwnBuy ? (lowest(CntdwnBars)-valuewhen(low==lowest(CntdwnBars), tr, 0)) : nz(riskLevel[1], low)

//---- Alert conditions ---------------------------
alertcondition(setupSell, title="Sell Setup", message="Sell Setup")
alertcondition(setupSellPerf, title="Sell Setup Perfected", message="Sell Setup Perfected")
alertcondition(setupBuy, title="Buy Setup", message="Buy Setup")
alertcondition(setupBuyPerf, title="Buy Setup Perfected", message="Buy Setup Perfected")
alertcondition(cntdwnSell, title="Sell Countdown", message="Sell Countdown")
alertcondition(cntdwnBuy, title="Buy Countdown", message="Buy Countdown")
alertcondition(cntdwnCountUpRecycle, title="Countdown Recycle Up", message="Countdown Recycle Up")
alertcondition(cntdwnCountDownRecycle, title="Countdown Recycle Down", message="Countdown Recycle Down")



//------------------------------------------------
//------------------------------------------------
//---- PLOTING SECTION ---------------------------
//------------------------------------------------
//------------------------------------------------

// TDST (Support/Resistance)
//   Use plot offset to move line back to beginning of Setup count...
stsp=plot(SetupTrendShow?setupTrendSupport:na, title="TDST Support",    style=stspStyle, color=stspColor, linewidth=1, offset=stspOffset)
strp=plot(SetupTrendShow?setupTrendResist:na,  title="TDST Resistance", style=strpStyle, color=strpColor, linewidth=1, offset=strpOffset)
// Shade area between support/resistance lines
fill(stsp, strp, title="TDST plot fill", color=(setupTrendSupport>setupTrendResist)?stpColorFlip:stpColorNormal)

// Show where equal price bars occur, if enabled
plotshape(SetupEqualEnable?(setupPriceEqual?true:na):na, title="Equal Price Compare", style=pepStyle, color=pepColor, location=pepLoc, size=pepSize, transp=Transp)

// Plot Countdown with numbers
plotshape(CntdwnShowCount?(cntdwnCountUpImp==1?true:na):na,    title="Countdown Up 1",    text="1",  style=ccupStyle, color=ccupColor, textcolor=red, location=ccupLoc, size=ccupSize, transp=Transp, editable=PlotEditEnable)
plotshape(CntdwnShowCount?(cntdwnCountUpImp==2?true:na):na,    title="Countdown Up 2",    text="2",  style=ccupStyle, color=ccupColor, textcolor=red, location=ccupLoc, size=ccupSize, transp=Transp, editable=PlotEditEnable)
plotshape(CntdwnShowCount?(cntdwnCountUpImp==3?true:na):na,   title="Countdown Up 3",   text="3", style=ccupStyle, color=ccupColor, textcolor=red, location=ccupLoc, size=ccupSize, transp=Transp, editable=PlotEditEnable)
plotshape(CntdwnShowCount?(cntdwnCountUpImp==4?true:na):na,   title="Countdown Up 4",   text="4", style=ccupStyle, color=ccupColor, textcolor=red, location=ccupLoc, size=ccupSize, transp=Transp, editable=PlotEditEnable)
plotshape(CntdwnShowCount?(cntdwnCountUpImp==5?true:na):na,   title="Countdown Up 5",   text="5", style=ccupStyle, color=ccupColor, textcolor=red, location=ccupLoc, size=ccupSize, transp=Transp, editable=PlotEditEnable)
plotshape(CntdwnShowCount?( (cntdwnCountUpImp>5) and (cntdwnCountUpImp<13) ?true:na):na,     title="Countdowns Up",  text="●",  style=ccupStyle, color=ccupColor, textcolor=red, location=ccupLoc, size=ccupSize, transp=Transp, editable=PlotEditEnable)
//plotshape(CntdwnShowCount?(cntdwnCountUpImp==8?true:na):na,    title="Countdown Up 8",    text="8",  style=ccupStyle, color=ccupColor, textcolor=red, location=ccupLoc, size=ccupSize, transp=Transp, editable=PlotEditEnable)
//plotshape(CntdwnShowCount?(cntdwnCountUpImp==9?true:na):na,    title="Countdown Up 9",    text="9",  style=ccupStyle, color=ccupColor, textcolor=red, location=ccupLoc, size=ccupSize, transp=Transp, editable=PlotEditEnable)
//plotshape(CntdwnShowCount?(cntdwnCountUpImp==10?true:na):na,   title="Countdown Up 10",   text="10", style=ccupStyle, color=ccupColor, textcolor=red, location=ccupLoc, size=ccupSize, transp=Transp, editable=PlotEditEnable)
//plotshape(CntdwnShowCount?(cntdwnCountUpImp==11?true:na):na,   title="Countdown Up 11",   text="11", style=ccupStyle, color=ccupColor, textcolor=red, location=ccupLoc, size=ccupSize, transp=Transp, editable=PlotEditEnable)
//plotshape(CntdwnShowCount?(cntdwnCountUpImp==12?true:na):na,   title="Countdown Up 12",   text="12", style=ccupStyle, color=ccupColor, textcolor=red, location=ccupLoc, size=ccupSize, transp=Transp, editable=PlotEditEnable)

plotshape(CntdwnShowCount?(cntdwnCountDownImp==1?true:na):na,  title="Countdown Down 1",  text="1",  style=ccdpStyle, color=ccdpColor, textcolor=red, location=ccdpLoc, size=ccdpSize, transp=Transp, editable=PlotEditEnable)
plotshape(CntdwnShowCount?(cntdwnCountDownImp==2?true:na):na,  title="Countdown Down 2",  text="2",  style=ccdpStyle, color=ccdpColor, textcolor=red, location=ccdpLoc, size=ccdpSize, transp=Transp, editable=PlotEditEnable)
plotshape(CntdwnShowCount?(cntdwnCountDownImp==3?true:na):na,  title="Countdown Down 3",  text="3",  style=ccdpStyle, color=ccdpColor, textcolor=red, location=ccdpLoc, size=ccdpSize, transp=Transp, editable=PlotEditEnable)
plotshape(CntdwnShowCount?(cntdwnCountDownImp==4?true:na):na,  title="Countdown Down 4",  text="4",  style=ccdpStyle, color=ccdpColor, textcolor=red, location=ccdpLoc, size=ccdpSize, transp=Transp, editable=PlotEditEnable)
plotshape(CntdwnShowCount?(cntdwnCountDownImp==5?true:na):na,  title="Countdown Down 5",  text="5",  style=ccdpStyle, color=ccdpColor, textcolor=red, location=ccdpLoc, size=ccdpSize, transp=Transp, editable=PlotEditEnable)
plotshape(CntdwnShowCount?( (cntdwnCountDownImp>5) and (cntdwnCountDownImp<13) ?true:na):na,   title="Countdowns Down",  text="●", style=ccdpStyle, color=ccdpColor, textcolor=red, location=ccdpLoc, size=ccdpSize, transp=Transp, editable=PlotEditEnable)
// plotshape(CntdwnShowCount?(cntdwnCountDownImp==8?true:na):na,  title="Countdown Down 8",  text="8",  style=ccdpStyle, color=ccdpColor, location=ccdpLoc, size=ccdpSize, transp=Transp, editable=PlotEditEnable)
// plotshape(CntdwnShowCount?(cntdwnCountDownImp==9?true:na):na,  title="Countdown Down 9",  text="9",  style=ccdpStyle, color=ccdpColor, location=ccdpLoc, size=ccdpSize, transp=Transp, editable=PlotEditEnable)
//plotshape(CntdwnShowCount?(cntdwnCountDownImp==10?true:na):na, title="Countdown Down 10", text="10", style=ccdpStyle, color=ccdpColor, location=ccdpLoc, size=ccdpSize, transp=Transp, editable=PlotEditEnable)
//plotshape(CntdwnShowCount?(cntdwnCountDownImp==11?true:na):na, title="Countdown Down 11", text="11", style=ccdpStyle, color=ccdpColor, location=ccdpLoc, size=ccdpSize, transp=Transp, editable=PlotEditEnable)
//plotshape(CntdwnShowCount?(cntdwnCountDownImp==12?true:na):na, title="Countdown Down 12", text="12", style=ccdpStyle, color=ccdpColor, textcolor=red, location=ccdpLoc, size=ccdpSize, transp=Transp, editable=PlotEditEnable)

// Plot Countdown qualify bar
plotshape(CntdwnShowCount?((cntdwnCountUpImp==CntdwnQualBar)or(cntdwnCountDownImp==CntdwnQualBar)?true:na):na, 
   title="Countdown Qual Bar", text=cqcpText, style=cqcpStyle, color=cqcpColor, location=cqcpLoc, size=cqcpSize, transp=Transp, editable=PlotEditEnable)
// Plot Countdown recycle
plotshape(CntdwnShowCount?(cntdwnCountUpRecycle or cntdwnCountDownRecycle):na, 
   title="Countdown Recycle", text=ccrpText, style=ccrpStyle, color=ccrpColor, location=ccrpLoc, size=ccrpSize, transp=Transp, editable=PlotEditEnable)

// Colors
greenColor = color(#2ab500, 0) //setup
redColor = color(#ce0202, 0) //countdown

// Plot setup buy/sell events
plotshape(setupSell, title="Sell Setup", text=sspText, textcolor=greenColor, color=na, location=sspLoc, size=sspSize, transp=Transp)
plotshape(setupBuy,  title="Buy Setup",  text=sbpText, textcolor=greenColor, color=na, location=sbpLoc, size=sbpSize, transp=Transp)
// Plot setup buy/sell perfected indicator
plotshape(setupSellPerf, title="Perfected Sell Setup", text=ssppText, textcolor=redColor, color=na, location=ssppLoc, size=ssppSize, transp=Transp)
plotshape(setupBuyPerf,  title="Perfected Buy Setup",  text=sbppText, textcolor=greenColor, color=na, location=sbppLoc, size=sbppSize, transp=Transp)


// Plot Countdown Sell/Buy Deferred event
plotshape(cntdwnSellDefer, title="Sell Countdown Deferred", text=csdpText, style=csdpStyle, color=csdpColor, location=csdpLoc, size=csdpSize, transp=Transp)
plotshape(cntdwnBuyDefer,  title="Buy Countdown Deferred",  text=cbdpText, style=cbdpStyle, color=cbdpColor, location=cbdpLoc, size=cbdpSize, transp=Transp)
// Plot Countdown Sell/Buy event
plotshape(CntdwnAggressive?na:cntdwnSell, title="Sell Countdown", text=cspText, style=cspStyle, textcolor=cspColor, color=na, location=cspLoc, size=cspSize, transp=Transp)
plotshape(CntdwnAggressive?na:cntdwnBuy,  title="Buy Countdown",  text=cbpText, style=cbpStyle, textcolor=cbpColor, color=na, location=cbpLoc, size=cbpSize, transp=Transp)

// Setups, last
// Plot Setup up/down counts
plotchar(SetupShowCount?((setupCountUp==1)?true:na):na,                    title="Setup Count Up 1",  char="1", color=scupColor, location=scupLoc, size=scupSize, transp=Transp, editable=PlotEditEnable)
plotchar(SetupShowCount?((setupCountUp==2)and(2<=SetupBars)?true:na):na,   title="Setup Count Up 2",  char="2", color=scupColor, location=scupLoc, size=scupSize, transp=Transp, editable=PlotEditEnable, show_last=scpShowLast)
plotchar(SetupShowCount?((setupCountUp==3)and(3<=SetupBars)?true:na):na,   title="Setup Count Up 3",  char="3", color=scupColor, location=scupLoc, size=scupSize, transp=Transp, editable=PlotEditEnable, show_last=scpShowLast)
plotchar(SetupShowCount?((setupCountUp==4)and(4<=SetupBars)?true:na):na,   title="Setup Count Up 4",  char="4", color=scupColor, location=scupLoc, size=scupSize, transp=Transp, editable=PlotEditEnable, show_last=scpShowLast)
plotchar(SetupShowCount?((setupCountUp==5)and(5<=SetupBars)?true:na):na,   title="Setup Count Up 5",  char="5", color=scupColor, location=scupLoc, size=scupSize, transp=Transp, editable=PlotEditEnable)
plotchar(SetupShowCount?((setupCountUp==6)and(6<=SetupBars)?true:na):na,   title="Setup Count Up 6",  char="6", color=scupColor, location=scupLoc, size=scupSize, transp=Transp, editable=PlotEditEnable, show_last=scpShowLast)
plotchar(SetupShowCount?((setupCountUp==7)and(7<=SetupBars)?true:na):na,   title="Setup Count Up 7",  char="7", color=scupColor, location=scupLoc, size=scupSize, transp=Transp, editable=PlotEditEnable)
plotchar(SetupShowCount?((setupCountUp==8)and(8<=SetupBars)?true:na):na,   title="Setup Count Up 8",  char="8", color=scupColor, location=scupLoc, size=scupSize, transp=Transp, editable=PlotEditEnable, show_last=scpShowLast)
plotchar(SetupShowCount?((setupCountUp==9)and(9<=SetupBars)?true:na):na,   title="Setup Count Up 9",  char="9", color=scupColor, location=scupLoc, size=scupSize, transp=Transp, editable=PlotEditEnable)
plotshape(SetupShowCount?((setupCountUp>SetupBars)?true:na):na, title="Setup Count Up > Last",  
   text=scuglpText, style=scuglpStyle, color=scuglpColor, location=scuglpLoc, size=scuglpSize, transp=Transp, editable=PlotEditEnable)

plotchar(SetupShowCount?((setupCountDown==1)?true:na):na,                    title="Setup Count Down 1",  char="1", color=scdpColor, location=scdpLoc, size=scdpSize, transp=Transp, editable=PlotEditEnable)
plotchar(SetupShowCount?((setupCountDown==2)and(2<=SetupBars)?true:na):na,   title="Setup Count Down 2",  char="2", color=scdpColor, location=scdpLoc, size=scdpSize, transp=Transp, editable=PlotEditEnable, show_last=scpShowLast)
plotchar(SetupShowCount?((setupCountDown==3)and(3<=SetupBars)?true:na):na,   title="Setup Count Down 3",  char="3", color=scdpColor, location=scdpLoc, size=scdpSize, transp=Transp, editable=PlotEditEnable, show_last=scpShowLast)
plotchar(SetupShowCount?((setupCountDown==4)and(4<=SetupBars)?true:na):na,   title="Setup Count Down 4",  char="4", color=scdpColor, location=scdpLoc, size=scdpSize, transp=Transp, editable=PlotEditEnable, show_last=scpShowLast)
plotchar(SetupShowCount?((setupCountDown==5)and(5<=SetupBars)?true:na):na,   title="Setup Count Down 5",  char="5", color=scdpColor, location=scdpLoc, size=scdpSize, transp=Transp, editable=PlotEditEnable)
plotchar(SetupShowCount?((setupCountDown==6)and(6<=SetupBars)?true:na):na,   title="Setup Count Down 6",  char="6", color=scdpColor, location=scdpLoc, size=scdpSize, transp=Transp, editable=PlotEditEnable, show_last=scpShowLast)
plotchar(SetupShowCount?((setupCountDown==7)and(7<=SetupBars)?true:na):na,   title="Setup Count Down 7",  char="7", color=scdpColor, location=scdpLoc, size=scdpSize, transp=Transp, editable=PlotEditEnable)
plotchar(SetupShowCount?((setupCountDown==8)and(8<=SetupBars)?true:na):na,   title="Setup Count Down 8",  char="8", color=scdpColor, location=scdpLoc, size=scdpSize, transp=Transp, editable=PlotEditEnable, show_last=scpShowLast)
plotchar(SetupShowCount?((setupCountDown==9)and(9<=SetupBars)?true:na):na,   title="Setup Count Down 9",  char="9", color=scdpColor, location=scdpLoc, size=scdpSize, transp=Transp, editable=PlotEditEnable)
plotshape(SetupShowCount?((setupCountDown>SetupBars)?true:na):na, title="Setup Count Down > Last",  
   text=scdglpText, style=scdglpStyle, color=scdglpColor, location=scdglpLoc, size=scdglpSize, transp=Transp, editable=PlotEditEnable)
